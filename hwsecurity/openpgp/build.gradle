apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

dependencies {
    api project(':hwsecurity:core')
    implementation project(':hwsecurity:provider')

    implementation 'org.bouncycastle:bcprov-jdk15to18:1.72'

    compileOnly project(':hwsecurity:ui')
    compileOnly 'com.google.android.material:material:1.12.0'

    compileOnly 'androidx.annotation:annotation:1.1.0'

    api 'com.google.auto.value:auto-value-annotations:1.6.5'
    annotationProcessor 'com.google.auto.value:auto-value:1.6.2'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.6'

    testImplementation 'androidx.annotation:annotation:1.1.0'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'org.mockito:mockito-core:2.18.0'
}

android {
    namespace = 'de.cotech.hw.openpgp'

    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        versionName rootProject.ext.hwSdkVersionName
        consumerProguardFiles 'hwsecurity-openpgp.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError = false
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-Xlint:unchecked,deprecation"]
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("$projectDir/../../hwsecurity.dev/content/reference"))

    moduleName.set("hwsecurity-openpgp")

    dokkaSourceSets {
        register("java") {
            sourceRoots.setFrom(file("src/main/java"))

            jdkVersion.set(8) // Used for linking to JDK documentation
            noStdlibLink.set(false) // Disable linking to online kotlin-stdlib documentation
            noJdkLink.set(true) // Disable linking to online JDK documentation
            noAndroidSdkLink.set(false) // Disable linking to online Android documentation

            perPackageOption {
                matchingRegex.set(".*\\.internal.*") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
        }
    }
}
