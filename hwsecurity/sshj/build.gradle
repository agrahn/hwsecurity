apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

dependencies {
    api project(':hwsecurity:core')

    implementation('com.hierynomus:sshj:0.33.0') { transitive = false }
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.72'
    implementation 'org.bouncycastle:bcpkix-jdk15to18:1.72'
    implementation "net.i2p.crypto:eddsa:0.3.0" // exposed transitive dependency in SSHJ

    // enable logging for debug build with /assets/logback.xml configuration
    debugImplementation 'org.slf4j:slf4j-api:1.7.30'
    debugImplementation 'com.github.tony19:logback-android:2.0.0'

    compileOnly 'androidx.annotation:annotation:1.1.0'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:2.18.0'
}

android {
    namespace 'de.cotech.hw.sshj'

    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion 26
        versionName rootProject.ext.hwSdkVersionName
        consumerProguardFiles 'hwsecurity-sshj.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("$projectDir/../../hwsecurity.dev/content/reference"))

    moduleName.set("hwsecurity-sshj")

    dokkaSourceSets {
        register("java") {
            sourceRoots.setFrom(file("src/main/java"))

            jdkVersion.set(8) // Used for linking to JDK documentation
            noStdlibLink.set(false) // Disable linking to online kotlin-stdlib documentation
            noJdkLink.set(true) // Disable linking to online JDK documentation
            noAndroidSdkLink.set(false) // Disable linking to online Android documentation
        }
    }
}
